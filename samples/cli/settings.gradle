
// Define a map for modules with custom project names
// Map directory names to custom project names
def customNamedProjects = [
    'all-module': 'sdk'
]

gradle.ext.includedModules = []

def rootProjectDir = new File('../..')

includeBuild(rootProjectDir) {
    dependencySubstitution {
        rootProjectDir.eachDir { File dir ->
            if (new File(dir, "build.gradle").exists()) {
                String projectName

                if (customNamedProjects.containsKey(dir.name)) {
                    projectName = customNamedProjects[dir.name]
                } else {
                    projectName = dir.name // Default to directory name
                }

                substitute module("net.accelbyte.sdk:$projectName") using project(":$projectName")
                gradle.ext.includedModules.add(projectName)
            }
        }
    }
}